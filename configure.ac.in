dnl Copyright (c) 2003-2006 Benedikt Meurer <benny@xfce.org>
dnl Copyright (c) 2010 Florian Rivoal <frivoal@xfce.org>
dnl

dnl Version information
m4_define([xwp_version_major], [0])
m4_define([xwp_version_minor], [6])
m4_define([xwp_version_micro], [1])
m4_define([xwp_version], [xwp_version_major().xwp_version_minor().xwp_version_micro()])

dnl Initial autoconf
AC_COPYRIGHT([Copyright (c) 2003-2006 Benedikt Meurer <benny@xfce.org>
Copyright (c) 2010 Florian Rivoal <frivoal@xfce.org>])
AC_INIT([xfce4-wavelan-plugin], [xwp_version], [xfce-dev@xfce.org])
AC_PREREQ([2.50])
AC_CONFIG_MACRO_DIRS([m4])

dnl Initial automake
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar no-dist-gzip foreign])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl Check for basic programs
AC_PROG_CC()
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([0.35.0])

LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl Check for standard header files
AC_HEADER_STDC()
AC_CHECK_HEADERS([math.h stdio.h stdlib.h string.h sys/types.h \
                  sys/socket.h sys/ioctl.h linux/types.h linux/if.h \
                  linux/wireless.h string.h \
                  ctype.h], [], [], [#include <sys/socket.h>])

AC_CHECK_LIBM
AC_SUBST(LIBM)

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl configure the panel plugin
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])

dnl Check for debugging support
XDT_FEATURE_DEBUG

AC_OUTPUT([
Makefile
panel-plugin/Makefile
po/Makefile.in
])
